/**************************************************************************
 Program:  Voices_q16_recode.sas
 Library:  Voices
 Project:  NeighborhoodInfo DC
 Author:   P. Tatian
 Created:  09/19/17
 Version:  SAS 9.2
 Environment:  Local Windows session (desktop)
 
 Description:  Read recoded Q16 data generated by Python program and
create final recoded data set. 

 Modifications: 10/18/17 LH replaced break var code with standard macro.
**************************************************************************/

%include "L:\SAS\Inc\StdLocal.sas";

** Define libraries **;
%DCData_lib( Voices )

filename fimport "L:\Libraries\Voices\Raw\Q16_recode.csv" lrecl=1000;

proc import out=Q16_recode
    datafile=fimport
    dbms=csv replace;
  datarow=2;
  getnames=yes;
  guessingrows=max;
run;

data Q16_recode_random;

  set Q16_recode;

  where recode not in ( '', 'NOMATCH' 'LOCATION');
  
  r = ranuni( -1 );
  
  format _all_ ;
  informat _all_ ;
  
run;

proc sort data=Q16_recode_random;
  by caseid respnum r;
run;

data Q16_recode_unq;

  set Q16_recode_random;
  by caseid respnum;
  
  if first.respnum;
  
  drop r;
  
run;

data location;
	
	set Q16_recode;

	where recode='LOCATION';

run;

proc sort location;

	by caseid respum;

data Q16_recode_unq_1 (drop=recode_l);

	merge Q16_recode_unq (in=a)
		  location (in=b keep=caseid respnum recode rename=(recode=recode_l));
	by caseid respnum;

	if recode='' and b=1 then recode=recode_l;

run;
data Voices_Q16_notyetrecoded (where=(respnum=.) drop=entity fuzzratio Q16_recode )
	 Voices_Q16_r1 (where=(respnum=1) drop=Q16_Text2 Q16_Text3 )
	 Voices_Q16_r2 (where=(respnum=2) drop=Q16_Text1 Q16_Text3 )
	 Voices_Q16_r3 (where=(respnum=3) drop=Q16_Text2 Q16_Text1 );

  merge
    Q16_recode_unq_1 
    Voices.VoicesDMVSurvey2017 (keep=caseid Q16_Text1 Q16_Text2 Q16_Text3);
  by caseid;

  rename recode=Q16_recode;
  
run;

proc transpose data=Voices_Q16_notyetrecoded out=Voices_Q16_notyetrecoded_1;

	by caseid;
	var Q16_Text1 Q16_Text2 Q16_Text3; 

run;
data Voices_Q16_notyetrecoded_2 (drop=_name_);

	set Voices_Q16_notyetrecoded_1 (drop=_label_);

	respnum=.;
	if _name_="Q16_Text1" then respnum=1;
	if _name_="Q16_Text2" then respnum=2;
	if _name_="Q16_Text3" then respnum=3;

run; 
data Voices_Q16_toreview;

	set Voices_Q16_r1 (rename=(Q16_Text1=Q16_Text))
		Voices_Q16_r2 (rename=(Q16_Text2=Q16_Text))
		Voices_Q16_r3 (rename=(Q16_Text3=Q16_Text))
		Voices_Q16_notyetrecoded_2(rename=(col1=Q16_Text));

		Q16_text=propcase(Q16_text);

		drop fuzzratio;
run;
%let list='TRAFFIC' 'TAXES' 'PARKING' 'ENTERTAINMENT' 'FOOD' 'CULTURE' 'MUSEUMS' 'MONUMENTS' 'PERFORM' 'ARTS' 'SPORTS' 'NIGHTLIFE' 'JOBS' 'PEOPLE' 'LOCATION' 'DIVERSITY'
		  'TRANSPORTATION' 'BUS' 'METRO' 'AIRPORTS' 'NATURE' 'COMMUNITY' 'SCHOOLS' 'WEATHER' 'LIFE' 'FAMILY' 'SHOPPING' 'SERVICES' 'CAPITAL' 'ECONOMY' 'NOTHING' 'SAFETY'
		  'HOUSING' 'DC' 'POLITICS' 'TRUMP' 'WALKABILITY' 'COSTS' 'NOISE' 'POLICE' 'POLLUTION' 'POVERTY' 'DRIVERS' 'GENTRIFICATION' 'TRASH' 'DRUGS' 'STRESS' 'CONSTRUCTION' 
		  'DISCRIMINATION' 'HOMELESSNESS' 'GANGS' 'TOURISTS' 'MONEY' 'BUGS' 'TERROR' 'RATS' '';

%macro printlist;

%do i=1 %to 57;
%let var=%scan(&list,&i.," ");

proc freq data=Voices_Q16_toreview;
tables Q16_Text ;
where  Q16_recode=&var.;
Title "Coded as &var"; 

run;
%end; 

%mend;

%printlist;
data Voices_Q16_recode;

  merge
    Q16_recode_unq 
    Voices.VoicesDMVSurvey2017 (keep=caseid weight dov_urban ppethm ppracem ppeducat ppincimp ppage ppgender pprent);
  by caseid;

 %make_break_vars_2017;

  rename recode=Q16_recode;
  
run;

proc format;

  value $Q16_r
    'ENTERTAINMENT' = "Food and entertainment options"
    'CULTURE' = "Culture and history"
    'JOBS' = "Jobs and career opportunities"
    'PEOPLE' = "Types of people who live here"
    'LOCATION' = "Location and accessibility"
    'DIVERSITY' = "Diversity"
    'TRANSPORTATION' = "Transportation options"
    'NATURE' = "Natural environment"
    'COMMUNITY' = "Community"
    'SCHOOLS' = "Schools and educational institutions"
    'WEATHER' = "Climate and weather"
    'LIFE' = "Lifestyle"
    'FAMILY' = "Family in area"
    'SHOPPING' = "Shopping options"
    'SERVICES' = "Services and amenities"
    'CAPITAL' = "It's the nation's capital"
    'ECONOMY' = "Economy"
    'NOTHING' = "Nothing"
    'SAFETY' = "Crime and safety"
    'HOUSING' = "Housing"
    'DC' = "Washington, DC"
    'POLITICS' = "Political culture"
    'WALKABILITY' = "Walkability"
    'COSTS' = "Cost of living";

  value $Q16_r_det
    'TRAFFIC' = "Traffic"
    'TAXES' = "Taxes"
    /*'TRUMP' = "Donald Trump"*/
    'PARKING' = "Parking"
    'ENTERTAINMENT' = "Entertainment"
    'FOOD' = "Food"
    'CULTURE' = "Culture"
    'MUSEUMS' = "Museums"
    'MONUMENTS' = "Monuments"
    'PERFORM' = "Performing arts"
    'ARTS' = "Arts"
    'SPORTS' = "Sports"
    'NIGHTLIFE' = "Nightlife"
    'JOBS' = "Jobs"
    'PEOPLE' = "People"
    'LOCATION' = "Location"
    'DIVERSITY' = "Diversity"
    'TRANSPORTATION', 'BUS' = "Transportation"
    'METRO' = "Metro"
    'AIRPORTS' = "Airports"    
    'NATURE' = "Nature"
    'COMMUNITY' = "Community"
    'SCHOOLS' = "Schools"
    'WEATHER' = "Weather"
    'LIFE' = "Lifestyle"
    'FAMILY' = "Family"
    'SHOPPING' = "Shopping"
    'SERVICES' = "Services"
    'CAPITAL' = "Nation's capital"
    'ECONOMY' = "Economy"
    'NOTHING' = "Nothing"
    'SAFETY' = "Crime"
    'HOUSING' = "Housing"
    'DC' = "Washington, DC"
    'POLITICS', 'TRUMP' = "Politics"
    'WALKABILITY' = "Walkability"
    'COSTS' = "Cost of living"
    'NOISE' = "Noise"
    'POLICE' = "Police"
    'POLLUTION' = "Pollution"
    'POVERTY' = "Poverty"
    'DRIVERS' = "Drivers"
    'GENTRIFICATION' = "Gentrification"
    'TRASH' = "Trash"
    'DRUGS' = "Drugs"
    'STRESS' = "Stress"
    'CONSTRUCTION' = "Construction"
    'DISCRIMINATION' = "Discrimination"
    'HOMELESSNESS' = "Homelessness"
    'GANGS' = "Gangs"
    'TOURISTS' = "Tourists"
    'MONEY' = "Money"
    'BUGS' = "Bugs"
    'TERROR' = "Terror threats"
    'RATS' = "Rats"
;




%Finalize_data_set( 
  data=Voices_Q16_recode,
  out=Voices_Q16_recode,
  outlib=Voices,
  label="VoicesDMV survey, 2017, Q16 recoded responses",
  sortby=caseid respnum,
  revisions=%str(New file.)
)

proc freq data=Voices_Q16_recode order=freq;
  weight weight;
  tables Q16_recode;  
  format Q16_recode $Q16_r_det.;
  title2 'All responses';
run;

proc freq data=Voices_Q16_recode order=freq;
  where respnum = 1;
  weight weight;
  tables Q16_recode;  
  format Q16_recode $Q16_r_det.;
  title2 'First response only';
run;

** Word cloud export **;

proc summary data=Voices_Q16_recode nway;
  class Q16_recode;
  var weight;
  output out=A sum=;
  format Q16_recode $Q16_r_det.;
run;

%File_info( data=A )

data Word_cloud;

  length Q16_recode $ 20 wc_weight 8;
  
  set A;
  
  wc_weight = round( weight );
  
  keep Q16_recode wc_weight;
  
run;

filename fexport "D:\DCData\Libraries\Voices\Raw\Voices_Q16_recode_wordcloud.txt" lrecl=256;

proc export data=Word_cloud
    outfile=fexport
    dbms=tab replace;

run;

filename fexport clear;

