/**************************************************************************
 Program:  Voices_q15_recode.sas
 Library:  Voices
 Project:  NeighborhoodInfo DC
 Author:   P. Tatian
 Created:  09/19/17
 Version:  SAS 9.4
 Environment:  Local Windows session (desktop)
 
 Description:  Read recoded Q15 data generated by Python program and
create final recoded data set. 

 Modifications: 10-18-17 LH replaced break vars code with standard macro. 
				10-23-17 LH modified recoding from Natural Language Processing
**************************************************************************/

%include "L:\SAS\Inc\StdLocal.sas";

** Define libraries **;
%DCData_lib( Voices )

filename fimport "L:\Libraries\Voices\Raw\Q15_recode.csv" lrecl=1000;

proc import out=Q15_recode
    datafile=fimport
    dbms=csv replace;
  datarow=2;
  getnames=yes;
  guessingrows=max;
run;

data Q15_recode_random;

  set Q15_recode;

  where recode not in ( '', 'NOMATCH' 'LOCATION');
  
  r = ranuni( -1 );
  
  format _all_ ;
  informat _all_ ;
  
run;

proc sort data=Q15_recode_random;
  by caseid respnum r;
run;

data Q15_recode_unq;

  set Q15_recode_random;
  by caseid respnum;
  
  if first.respnum;
  
  drop r;
  
run;
data location;
	
	set Q15_recode; 

	where recode = 'LOCATION';

run; 
proc sort data=location;

	by caseid respnum;

data Q15_recode_unq_1 (drop=recode_l);

	merge Q15_recode_unq (in=a)
		  location (in=b keep=caseid respnum recode rename=(recode=recode_l));
	by caseid respnum;

	if recode='' and b=1 then recode=recode_l;

run;

data Voices_Q15_notyetrecoded (where=(respnum=.) drop=entity fuzzratio Q15_recode )
	 Voices_Q15_r1 (where=(respnum=1) drop=Q15_Text2 Q15_Text3 )
	 Voices_Q15_r2 (where=(respnum=2) drop=Q15_Text1 Q15_Text3 )
	 Voices_Q15_r3 (where=(respnum=3) drop=Q15_Text2 Q15_Text1 );

  merge
    Q15_recode_unq_1 
    Voices.VoicesDMVSurvey2017 (keep=caseid Q15_Text1 Q15_Text2 Q15_Text3/*weight dov_urban ppethm ppracem ppeducat ppincimp ppage ppgender pprent*/);
  by caseid;

  rename recode=Q15_recode;
  
run;

proc transpose data=Voices_Q15_notyetrecoded out=Voices_Q15_notyetrecoded_1;

	by caseid;
	var Q15_Text1 Q15_Text2 Q15_Text3; 

run;
data Voices_Q15_notyetrecoded_2 (drop=_name_);

	set Voices_Q15_notyetrecoded_1 (drop=_label_);

	respnum=.;
	if _name_="Q15_Text1" then respnum=1;
	if _name_="Q15_Text2" then respnum=2;
	if _name_="Q15_Text3" then respnum=3;

run; 
data Voices_Q15_toreview;

	set Voices_Q15_r1 (rename=(Q15_Text1=Q15_Text))
		Voices_Q15_r2 (rename=(Q15_Text2=Q15_Text))
		Voices_Q15_r3 (rename=(Q15_Text3=Q15_Text))
		Voices_Q15_notyetrecoded_2(rename=(col1=Q15_Text));

		Q15_text=propcase(Q15_text);

		drop fuzzratio;
run;
%let list='ENTERTAINMENT' 'FOOD' 'CULTURE' 'MUSEUMS' 'MONUMENTS' 'PERFORM' 'ARTS' 'SPORTS' 'NIGHTLIFE' 'JOBS' 'PEOPLE' 'LOCATION' 'DIVERSITY' 'TRANSPORTATION' 'BUS' 'METRO'
    'AIRPORTS' 'NATURE' 'COMMUNITY' 'SCHOOLS' 'WEATHER' 'LIFE' 'FAMILY' 'SHOPPING' 'SERVICES' 'CAPITAL' 'ECONOMY' 'NOTHING' 'SAFETY' 'HOUSING' 'DC' 'POLITICS' 'WALKABILITY'
    'COSTS' '';

%macro printlist;

%do i=1 %to 35;
%let var=%scan(&list,&i.," ");

proc freq data=Voices_Q15_toreview;
tables Q15_Text ;
where  Q15_recode=&var.;
Title2 "Coded as &var"; 

run;
%end; 

title2;

%mend;

%printlist;

data Voices_Q15_newrecode (drop=diversity culture politics capital monuments transpo metro jobs food nightlife entertain services people);

	set Voices_Q15_toreview;

			culture=index(Q15_text,"Cultural Activ");
			culture=culture+index(Q15_text,"Cultural Events");
			culture=culture+index(Q15_text,"Cultural Sites");
			culture=culture+index(Q15_text,"Cultural Venues");
			culture=culture+index(Q15_text,"Cultural Places");
			culture=culture+index(Q15_text,"Cultyural Events");
			culture=culture+index(Q15_text,"Cutural Events");
			culture=culture+index(Q15_text,"Access To Cultural Institutions"); *was in location;
			culture=culture+index(Q15_text,"Access To Cultural/Historical Resources");*was in location;
			culture=culture+index(Q15_text,"Access To Free Cultural");*was in location;
			culture=culture+index(Q15_text,"Cultural Access");*was in location;
			culture=culture+index(Q15_text,"Proximity To Cultural Institutions");*was in location;
			culture=culture+index(Q15_text,"Proximity To Cultural And Historic");*was in location;

			politics=index(Q15_text,"Dynamic Politics");
			politics=politics+index(Q15_text,"Proximity To Political Protest");
			politics=politics+index(Q15_text,"Political Climate"); *was in weather;
			politics=politics+index(Q15_text,"Exciting Atmosphere Politically"); *was in life;
			politics=politics+index(Q15_text,"Liberal/Progressive"); *was in life;
			politics=politics+index(Q15_text,"Liberal Government"); *was in life;

			capital=index(Q15_text,"Home Of The Presidents");
			capital=capital+index(Q15_text,"The Proximity To The Center Of Us House");
			capital=capital+index(Q15_text,"Access To Political Leader");
			capital=capital+index(Q15_text,"Access To Politicians");
			capital=capital+index(Q15_text,"Federal Legislative Process");
			capital=capital+index(Q15_text,"Access To Government Buildings");
			capital=capital+index(Q15_text,"Political Events");
			capital=capital+index(Q15_text,"Being At The Center Of Politics");
			capital=capital+index(Q15_text,"Being Close To National Politics");

			monuments=index(Q15_text,"National History Events");
			monuments=monuments+index(Q15_text,"Access To Educational/Historical");
			monuments=monuments+index(Q15_text,"Access To Historic Sites");
			monuments=monuments+index(Q15_text,"Access To Historic Loca");
			monuments=monuments+index(Q15_text,"Access To Historical Sites");
			monuments=monuments+index(Q15_text,"Access To Historical/Cult");
			monuments=monuments+index(Q15_text,"Near Many Historical Loca");
			monuments=monuments+index(Q15_text,"Access To Historical And Cult");
			monuments=monuments+index(Q15_text,"Access To Historical & Cult");
			monuments=monuments+index(Q15_text,"Access To Historical Sites");
			monuments=monuments+index(Q15_text,"Access To Historical Loca");
			monuments=monuments+index(Q15_text,"Proximity To Historical Sites");


			jobs=index(Q15_text,"Legal Activities"); *was in entertainment;
			jobs=jobs+index(Q15_text,"Access To Different Industr"); *was in location;
			jobs=jobs+index(Q15_text,"Access To Iobs"); *was in location;
			jobs=jobs+index(Q15_text,"Asccess To Work As A Contractor"); *was in location;
			jobs=jobs+index(Q15_text,"Employment Opportunities Related To"); *was in diversity;
			if Q15_text in("Government Employment" "Internships" "Nice Job Placements") then jobs=1; 


			diversity=index(Q15_text,"Different Cultu");
			diversity=diversity+index(Q15_text,"Diverse Cultu");
			diversity=diversity+index(Q15_text,"Diverese Cultu");
			diversity=diversity+index(Q15_text,"Diversity Of Cultu");
			diversity=diversity+index(Q15_text,"Diversity Of People");
			diversity=diversity+index(Q15_text,"Encountering People From Differ");
			diversity=diversity+index(Q15_text,"Countless Cultu");
			diversity=diversity+index(Q15_text,"The Culture Range");
			diversity=diversity+index(Q15_text,"Differnt Cult");
			diversity=diversity+index(Q15_text,"Varies Cultu");
			diversity=diversity+index(Q15_text,"Variety Of Cultu");
			diversity=diversity+index(Q15_text,"Variety Of Peopl");
			diversity=diversity+index(Q15_text,"People From Different Countries");
			diversity=diversity+index(Q15_text,"People From Different Places");
			diversity=diversity+index(Q15_text,"Cultural Mix");
			diversity=diversity+index(Q15_text,"Different Groups Of Peop");
			diversity=diversity+index(Q15_text,"Different Types Of Peop");
			diversity=diversity+index(Q15_text,"Diverse Group Of Peop");
			diversity=diversity+index(Q15_text,"Diverse Peop");
			diversity=diversity+index(Q15_text,"Diverse Popul");
			diversity=diversity+index(Q15_text,"Diverse, Interesting People");
			diversity=diversity+index(Q15_text,"Diverse/International");
			diversity=diversity+index(Q15_text,"Diversified Pop");
			diversity=diversity+index(Q15_text,"Diversity Of The Peop");
			diversity=diversity+index(Q15_text,"Diversity Of The Popul");
			diversity=diversity+index(Q15_text,"Ethnically Diverse");
			diversity=diversity+index(Q15_text,"Know People From Other");
			diversity=diversity+index(Q15_text,"Diversity Of Diffent People");
			diversity=diversity+index(Q15_text,"Mix Of People");
			diversity=diversity+index(Q15_text,"Interacting W/People From All");
			diversity=diversity+index(Q15_text,"Interesting Cross-Section Of Peop");
			diversity=diversity+index(Q15_text,"It Has Many Races Of");
			diversity=diversity+index(Q15_text,"International City");
			diversity=diversity+index(Q15_text,"Meeting All Kinds Of Peopl");
			diversity=diversity+index(Q15_text,"Multi Cultur");
			diversity=diversity+index(Q15_text,"Multi Ethnic");
			diversity=diversity+index(Q15_text,"Nice Mix Of People");
			diversity=diversity+index(Q15_text,"People Come From All Over");
			diversity=diversity+index(Q15_text,"People From All Over");
			diversity=diversity+index(Q15_text,"People From All Around");
			diversity=diversity+index(Q15_text,"People From Diverse");
			diversity=diversity+index(Q15_text,"People Of All Types");
			diversity=diversity+index(Q15_text,"Large Array Of People");
			diversity=diversity+index(Q15_text,"The Mixture Of People");
			diversity=diversity+index(Q15_text,"Different Color Skins");
			diversity=diversity+index(Q15_text,"Cosmopolitan Population");
			diversity=diversity+index(Q15_text,"Diverse Community");
			diversity=diversity+index(Q15_text,"Diverse Ethnicity");
			diversity=diversity+index(Q15_text,"Ethnic Neighborhoods/Cultures");
			diversity=diversity+index(Q15_text,"Very Diverse Neighborhoods");

			food=index(Q15_text,"Food From");
			food=food+index(Q15_text,"Diversity Of Cuisine"); 
			food=food+index(Q15_text,"Diversity Of Food"); 


			nightlife=index(Q15_text,"Dance Clube");
			nightlife=nightlife+index(Q15_text,"Going To The Clubs");
			nightlife=nightlife+index(Q15_text,"Good Night Life");


			services=index(Q15_text,"Free Help Services");
			services=services+index(Q15_text,"So Many Resources");
			services=services+index(Q15_text,"Access To Good Medical");
			services=services+index(Q15_text,"Access To Most Household Services");
			services=services+index(Q15_text,"Access To My Karate");
			services=services+index(Q15_text,"Access To Resources");
			services=services+index(Q15_text,"Access To Top Medical");
			services=services+index(Q15_text,"Full Social Security"); *was in safety; 

			entertain=index(Q15_text,"Access To Recreational Facilities");
			entertain=entertain+index(Q15_text,"Access To Recreational Spots");
			entertain=entertain+index(Q15_text,"Access To Recreation Zone");
			if Q15_text="A Lot Of Amusment Parks" then entertain=1;
			if Q15_text="Bars, Clubs, Music Venues, Variety in Dining" then entertain=1; *was community;
			if Q15_text in("A Lot Of Things To Do" "A Lot To Do" "All Of The Family Activities Available" "Always Lots To Do" "Entertainment Experience" "It Has Lots Of Stuff To Do"
							"Boating" "Many Things To Do" "Lots Of Free Things To Do" "Lots Of Fun Stuff For Kids" "Lots Of Stuff To Do" "Lots Of Things To Do" "Lots To Do" "Lots To See"
							"Places To Go" "Places To See" "Plenty Of Things To Do" "So Many Things To Do" "So Much To Do" "The Attractions" "There Is Entertainment Spritz Thouhout The Dmv"
							"Things To Do" "Things To See" "Tons Of Things To Do" "Lot To Do" "Movies") then entertain=1; *was not matched;

			metro=index(Q15_text,"Metro Accesibility");
			metro=metro+index(Q15_text,"Metro Accessiblity");
			if Q15_text in("Easy Metro Access" "Metro Access" ) then metro=1;
			if Q15_recode='' and Q15_Text in("Metro" "Metro Accessible") then metro=1;

			transpo=index(Q15_text,"Public Transit"); *was metro;
			if Q15_text in("Nice Transit" "Mass Transit" "Transit" "Easy Travel & Transit" "Calles" "Decent Transporation Links" "Easy To Get Around" "Easy To Travel To Other Places"
							"Transportstio" "Travel Options" "It's Easy To Get Around" "Ability To Bike To Work" "Access To Transport Options") then transpo=1;

			people=index(Q15_text,"Affluent Communities");
			people=people+index(Q15_text,"Citizens Are Highly Educated"); 
			people=people+index(Q15_text,"People With Money"); 

			if culture > 0 then Q15_recode="CULTURE";
			else if politics > 0 then Q15_recode="POLITICS";
			else if capital > 0 then Q15_recode="CAPITAL";
			else if monuments > 0 then Q15_recode="MONUMENTS";
			else if jobs > 0 then Q15_recode="JOBS";
			else if diversity > 0 then Q15_recode="DIVERSITY";
			else if food > 0 then Q15_recode="FOOD";
			else if nightlife > 0 then Q15_recode="NIGHTLIFE";
			else if services > 0 then Q15_recode="SERVICES";
			else if entertain > 0 then Q15_recode="ENTERTAINMENT";
			else if people > 0 then Q15_recode="PEOPLE";
			else if metro > 0 then Q15_recode="METRO";
			else if transpo > 0 then Q15_recode="TRANSPORTATION";
			

			if Q15_Text="Networking" then Q15_recode="COMMUNITY"; *was jobs;
			if Q15_text in("Communities" "In Good Nabourhood" "Convenient Religious Facilities" ) then Q15_recode="COMMUNITY"; 
			if Q15_text="Mix Of Local And Urban Communities" then Q15_recode="COMMUNITY";*was diversity;

			if Q15_Text="Police Does Its Work" then Q15_recode="SAFETY"; *was jobs;
			if Q15_text in("Safe Community" "Safe Neighborhoods" "Safety For Children" "Safe" "Safe Area" "Safe Place" "Seguridad" "Segurity" "Mucha Seguridad"
						   "Generally Safe") then Q15_recode="SAFETY";

			if Q15_Text="Access To International Flights Out Of Iad" then Q15_recode="AIRPORTS";
			if Q15_Text="Proximity To Dulles Airport" then Q15_recode="AIRPORTS";

			if Q15_text="Diversity Of Cultural Opportunities" then Q15_recode="CULTURE";*was diversity;
			if Q15_text in("Cultural Amenities" "Excellent Cultural Amenities") then Q15_recode="CULTURE";*was services;
			if Q15_text in("Cultural Things" "Cultural Resources" "Cultured" "Culturally Strong" "Cultural/Historical" "Cultural Opportunities" "Excellent Cultural Venues"
							"Nearby Cultural Facilities" "Free Cultural Opportunities" "The Cultural Amenities" "Large Amount Of Many Types Of Culteral And Historical Activities"
						    ) then Q15_recode="CULTURE";

			if Q15_text in ("Muesm M" "Washington Dc Museums" "Access To Museums And Monuments In Dc" "Museums & Activities" "Museums And Activities"
							"Nice Activities, Museums") then Q15_recode="MUSEUMS";

			if Q15_text in("Ability To Walk To Area Bus And Rest" "Walkable Communities, Good Transit" "Walkable, Fun Neighborhoods" "Walk Every Where") then Q15_recode="WALKABILITY"; 

			if Q15_text="The National Mall Is Just A Half Hour Away By Metro" then Q15_recode="MONUMENTS"; *was metro;
			if Q15_text in("Fountains" "Historic" "Historic Places To Visit" "Historic Resources" "Historical Sites" "Historically Strong" "Historical Locations" "Martin Luther King Mermeriol"
							"National Mall Very Nice" "Sight Seeing" "Tourtist"  "The Historic Buildings, Histo" "It Is Full Of Historical Information And Sites.") then Q15_recode="MONUMENTS";

			if Q15_text="Clubs/Restaurants" then Q15_recode="NIGHTLIFE";

			if Q15_text in("Good School And Parks In My Neighborhood" "Good Schools For Children" "Good Schools for Son" "Educationally Strong" "Quality Montgomery Schools") then Q15_recode="SCHOOLS";

			if Q15_text in("4 Different Weather Patterns" "El Clima" "Few Natural Disasters" "Hot" "The Heat" "The Rain" "Variable Weather Conditions W/O Excess") then Q15_recode="WEATHER";

			if Q15_text in("Affordable" "And It Is Kind Of Not To Pricy" "Cheaper Than New York" "Expensive") then Q15_recode="COSTS";

			if Q15_text in("Area Urbana" "Centrally Located For Travel Abroad" "Close To Big Cities" "Close To City" "Close To DMV" "Close To Everything" "Close To Lots Of Stuff"
							"Close To Pennsylvania" "Close To Places" "Close To Two Major Cities" "Close To West Virginia" "Close To Work" "Connivence" "Convenient" "Convenient To Everything"
							"Convient" "Every Thing Is Close" "Everything Is Convient" "Everything Is Close" "Everything Is Close By" "Halfway Between Ca And France"
							"Access To Other Communities" "It Is Convient" "Its Close To Everything I Need" "Near The City" "Close To Dmv" "Everything In Convient") then Q15_recode="LOCATION";

			if Q15_text in("Avaiable Facilities" "Excellent Medical Facilities" "Outstanding Medical Facilities") then Q15_recode="SERVICES";

			if Q15_text="Prestigious African American Communities" then Q15_recode="PEOPLE"; *was diversity;
			if Q15_text in("Babes" "Buenas Personas" "Buenas Persona" "Human Quality" "Gilrs" "Everyone Knows Everyone" "Son Personas Unidas") then Q15_recode="PEOPLE";

			if Q15_text in("Beautiful" "Beautiful Areas" "Es Muy Bonito" "El Paisaje" "Green Spaces" "Proximity To Scenic Areas" "Lovely" "Pretty" "The Views" "Tree Lined Streets"
							"The Natural Areas Surrounding Dc For Activities") then Q15_recode="NATURE";

			if Q15_text in("Centros Comerciales" "Economically Vabraint" "Employment Rates Seem High" "Entrepreneurial Experience" "Economically Diverse" "Income Ranges") then Q15_recode="ECONOMY";

			if Q15_text in("Intellectual Climate" "Social Climate" "Ambiente" "Calm" "Chill" "Choice Of Living" "Cool" "Crowded" "Crowd" "Es Muy Calida El Area" "Friendly"
							"Good Social Scene" "Happy" "Harmony" "Excited" "Fair" "It Is A Very Adventurous City" "It's Cool" "It's Fun" "It's Nice" "Kindness" "Peaceful" "Quiet"
							"Unique") then Q15_recode="LIFE";

			if Q15_text in("Cosmopolitan" "Culturally Diverse" "Culturally Diverse." "Diverse" "Good Choices In Diverse Places To Be Around" "Multicultural" "Very Diverse")then Q15_recode="DIVERSITY";
			if Q15_text in("Eating" "Fresh Fish" "Mumbo Sauce" "Places To Eat") then Q15_recode="FOOD";
			if Q15_text in("First Hand Law Making" "Government Center" "It Is Us Capitol") then Q15_recode="CAPITAL";

			if Q15_text in("Go Go" "Easy Access To Live Theater Performances" "Music Concerts") then Q15_recode="PERFORM";
			if Q15_text in("Living With My Spouse") then Q15_recode="FAMILY";
			if Q15_text in("New Developments Are Rising Daily") Then Q15_recode="HOUSING";
			if Q15_text in("Plenty Of Places To Buy Things Needed") Then Q15_recode="SHOPPING";


run;

%macro printlist_new;

%do i=1 %to 35;
%let var=%scan(&list,&i.," ");

proc freq data=Voices_Q15_newrecode;
tables Q15_Text ;
where  Q15_recode=&var.;
Title2 "Coded as &var"; 

run;
%end; 

title2;

%mend;

%printlist_new;

proc sort data=Voices_Q15_newrecode;
	by caseid;

data Voices_Q15_recode;

	merge Voices_Q15_newrecode (drop=entity)
		  Voices.VoicesDMVSurvey2017 (keep=caseid weight dov_urban ppethm ppracem ppeducat ppincimp ppage ppgender pprent);
	by caseid;

		%make_break_vars_2017;

	label respnum="Survey Response Number"
		  Q15_recode="Q15 recoded response categories: 3 best things about living in Washington area"
		  Q15_text="Q15 actual response: 3 best things about living in Washington area"
		  ;

run;


 
proc format;

  value $Q15_r
    'ENTERTAINMENT' = "Food and entertainment options"
    'CULTURE' = "Culture and history"
    'JOBS' = "Jobs and career opportunities"
    'PEOPLE' = "Types of people who live here"
    'LOCATION' = "Location and accessibility"
    'DIVERSITY' = "Diversity"
    'TRANSPORTATION' = "Transportation options"
    'NATURE' = "Natural environment"
    'COMMUNITY' = "Community"
    'SCHOOLS' = "Schools and educational institutions"
    'WEATHER' = "Climate and weather"
    'LIFE' = "Lifestyle"
    'FAMILY' = "Family in area"
    'SHOPPING' = "Shopping options"
    'SERVICES' = "Services and amenities"
    'CAPITAL' = "It's the nation's capital"
    'ECONOMY' = "Economy"
    'NOTHING' = "Nothing"
    'SAFETY' = "Crime and safety"
    'HOUSING' = "Housing"
    'DC' = "Washington, DC"
    'POLITICS' = "Political culture"
    'WALKABILITY' = "Walkability"
    'COSTS' = "Cost of living";

  value $Q15_r_det
    'ENTERTAINMENT' = "Entertainment"
    'FOOD' = "Food"
    'CULTURE' = "Culture"
    'MUSEUMS' = "Museums"
    'MONUMENTS' = "Monuments"
    'PERFORM' = "Performing arts"
    'ARTS' = "Arts"
    'SPORTS' = "Sports"
    'NIGHTLIFE' = "Nightlife"
    'JOBS' = "Jobs"
    'PEOPLE' = "People"
    'LOCATION' = "Location"
    'DIVERSITY' = "Diversity"
    'TRANSPORTATION', 'BUS' = "Transportation"
    'METRO' = "Metro"
    'AIRPORTS' = "Airports"    
    'NATURE' = "Nature"
    'COMMUNITY' = "Community"
    'SCHOOLS' = "Schools"
    'WEATHER' = "Weather"
    'LIFE' = "Lifestyle"
    'FAMILY' = "Family"
    'SHOPPING' = "Shopping"
    'SERVICES' = "Services"
    'CAPITAL' = "Nation's capital"
    'ECONOMY' = "Economy"
    'NOTHING' = "Nothing"
    'SAFETY' = "Crime & safety"
    'HOUSING' = "Housing"
    'DC' = "Washington, DC"
    'POLITICS' = "Political culture"
    'WALKABILITY' = "Walkability"
    'COSTS' = "Cost of living";



%Finalize_data_set( 
  data=Voices_Q15_recode,
  out=Voices_Q15_recode,
  outlib=Voices,
  label="VoicesDMV survey, 2017, Q15 recoded responses",
  sortby=caseid respnum,
  revisions=%str(Added labels.)
)

proc freq data=Voices_Q15_recode order=freq;
  weight weight;
  tables Q15_recode;  
  format Q15_recode $Q15_r_det.;
run;

** Word cloud export **;

proc summary data=Voices_Q15_recode nway;
  class Q15_recode;
  var weight;
  output out=A sum=;
  format Q15_recode $Q15_r_det.;
run;

%File_info( data=A )

data Word_cloud;

  length Q15_recode $ 20 wc_weight 8;
  
  set A;
  
  wc_weight = round( weight );
  
  keep Q15_recode wc_weight;
  
run;

filename fexport "D:\DCData\Libraries\Voices\Raw\Voices_q15_recode_wordcloud.txt" lrecl=256;

proc export data=Word_cloud
    outfile=fexport
    dbms=tab replace;

run;

filename fexport clear;

